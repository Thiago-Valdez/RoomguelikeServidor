// build.gradle (raíz de EscapeRoomguelike)

buildscript {
  repositories {
    mavenCentral()
    maven { url 'https://s01.oss.sonatype.org' }
    gradlePluginPortal()
    mavenLocal()
    google()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
  }
  dependencies {
    // classpath y plugins globales; nada de implementation acá
    classpath "io.github.fourlastor:construo:1.7.1"
    if (enableGraalNative == 'true') {
      classpath "org.graalvm.buildtools.native:org.graalvm.buildtools.native.gradle.plugin:0.9.28"
    }
  }
}

// Config común a todos los proyectos
allprojects {
  apply plugin: 'eclipse'
  apply plugin: 'idea'

  idea {
    module {
      outputDir file('build/classes/java/main')
      testOutputDir file('build/classes/java/test')
    }
  }
}

// Config común a subproyectos Java (core, lwjgl3, etc.)
configure(subprojects) {
  apply plugin: 'java-library'

  java {
    toolchain {
      languageVersion = JavaLanguageVersion.of(17)
    }
  }

  tasks.withType(JavaCompile).configureEach {
    options.release = 17
    options.incremental = true
  }

  // Generar assets.txt
  tasks.register('generateAssetList') {
    inputs.dir("${project.rootDir}/assets/")
    File assetsFolder = new File("${project.rootDir}/assets/")
    File assetsFile = new File(assetsFolder, "assets.txt")
    assetsFile.delete()

    fileTree(assetsFolder).collect { assetsFolder.relativePath(it) }.sort().each {
      assetsFile.append(it + "\n")
    }
  }
  processResources.dependsOn 'generateAssetList'

  tasks.withType(Test).configureEach {
    useJUnitPlatform()
  }
}

subprojects {
  version = (project.findProperty("projectVersion") ?: "1.0.0").toString()
  ext.appName = 'EscapeRoomguelike'

  repositories {
    mavenCentral()
    maven { url 'https://s01.oss.sonatype.org' }
    mavenLocal()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'https://jitpack.io' }
  }
}

// Nombre del proyecto raíz en Eclipse
eclipse.project.name = 'EscapeRoomguelike-parent'
